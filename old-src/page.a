
import lib/loaders/ipx.a
import lib/loaders/ivx.a

import lib/devices/graphics/surface.a
import lib/devices/graphics/font.a

import lib/devices/graphics/shader.a
import lib/devices/graphics/gl.a

import lib/utils/camera.a

var CONSOLE_PAGE_HIDDEN = 0;
var CONSOLE_PAGE_SHOWN  = 1;

Surface->var card_shadow = new(Surface).construct();
card_shadow.texture Ipx.create_texture(CONSOLE_CARD_SHADOW_PATH);

Font->var main_font = new(Font).construct(CONSOLE_FONT_PATH);

Shader->var model_shader = new(Shader).construct("attrib vec4 attrib_vertex_position;\nattrib vec2 attrib_texture_coords;\nattrib vec3 attrib_vertex_normal;\n\ninterp vec4 interp_vertex_position;\ninterp vec2 interp_texture_coords;\ninterp vec3 interp_normal;\ninterp_normal = attrib_vertex_normal;\ninterp_vertex_position = attrib_vertex_position;\ninterp_texture_coords = attrib_texture_coords;\n\ninform mat4 inform_mvp_matrix;\nreturn inform_mvp_matrix * interp_vertex_position;\n", "interp vec4 interp_vertex_position;\ninterp vec2 interp_texture_coords;\ninterp vec3 interp_normal;\ninform tex2 inform_texture;\n\nvec4 colour = sample(inform_texture, interp_texture_coords);\n\nvec3 light_vector = vec3(2.0, 1.0, 0.5) - interp_vertex_position.xyz;\ncolour.xyz = mix(colour.xyz / 5.0, colour.xyz, max(0.0, dot(normalize(interp_normal), normalize(light_vector))));\n\nreturn colour;\n");
var model_shader_mvp_matrix = model_shader.find("inform_mvp_matrix");
var model_shader_texture = model_shader.find("inform_texture");

class Console_page {
	var target_visibility;
	fixed var transition;
	
	Texture->var title_texture;
	Surface->var title_surface;
	
	fixed var title_width;
	fixed var title_height;
	
	Texture->var paragraph_texture;
	Surface->var paragraph_surface;
	
	fixed var paragraph_width;
	fixed var paragraph_height;
	
	class Console_card {
		Texture->var picture_texture;
		Surface->var picture_surface;
		
		Texture->var text_texture;
		Surface->var text_surface;
		
		fixed var text_width;
		fixed var text_height;
		
		var select_function;
		var select_argument;
		
		var next_platform_command;
		var next_page;
	}
	
	Camera->var model_camera;
	Ivx->var model_ivx;
	Texture->var model_texture;
	
	var card_count;
	var cards;
	
	fixed var target_card_selection;
	fixed var card_selection;
	
	var next_page;
	var prev_page;
	
	var next_function;
	var next_argument;
	
	var prev_function;
	var prev_argument;
	
	var block_next;
	var block_prev;
	
	func show(var prev_page) {
		if (prev_page) self.prev_page = prev_page;
		self.target_visibility = CONSOLE_PAGE_SHOWN;
	}
	
	func hide {
		self.target_visibility = CONSOLE_PAGE_HIDDEN;
	}
	
	func draw(var fps) {
		if (self.target_visibility == CONSOLE_PAGE_SHOWN) {
			if (not self.block_next and console_input_button_down()) {
				if (self.card_count) {
					Console_card->var card = ?(self.cards + 8 * self.target_card_selection);
					
					if (card.next_platform_command) {
						platform_command card.next_platform_command;
						
					} else if (card.next_page) {
						self.hide();
						Console_page->(card.next_page).show(self);
					}
				}
				
				if (self.target_visibility == CONSOLE_PAGE_SHOWN) {
					self.hide();
					
					if (self.next_page) {
						Console_page->(self.next_page).show(self);
					}
				}
				
				if (self.next_function and self.target_visibility == CONSOLE_PAGE_HIDDEN) {
					self.next_function(self.next_argument);
				}
			} else if (not self.block_prev and console_input_button_up()) {
				if (self.target_visibility == CONSOLE_PAGE_SHOWN) {
					self.hide();
					
					if (self.prev_page) {
						Console_page->(self.prev_page).show(0);
					}
				}
				
				if (self.prev_function and self.target_visibility == CONSOLE_PAGE_HIDDEN) {
					self.prev_function(self.prev_argument);
				}
			}
		}
		
		if (self.target_visibility == CONSOLE_PAGE_SHOWN and self.card_count) {
			var selection_changed = 0;
			
			if (console_input_button_right()) {
				self.target_card_selection = self.target_card_selection + 1;
				selection_changed = 1;
				
			} if (console_input_button_left()) {
				self.target_card_selection = self.target_card_selection - 1;
				selection_changed = 1;
			}
			
			if (self.target_card_selection < 0) self.target_card_selection = self.card_count - 1;
			else if (self.target_card_selection >= self.card_count) self.target_card_selection = 0;
			
			if (selection_changed) {
				Console_card->var card = ?(self.cards + 8 * self.target_card_selection);
				
				if (card.select_function) {
					card.select_function card.select_argument;
				}
			}
		}
		
		if (self.target_visibility == CONSOLE_PAGE_HIDDEN and self.transition < 0.0625) return;
		self.transition = self.transition + (self.target_visibility * 1.0 - self.transition) * 7 / fps;
		fixed var transition = self.transition;
		
		var layer = current_layer;
		current_layer = current_layer + 12;
		
		if (self.card_count) {
			self.card_selection = self.card_selection + (self.target_card_selection * 1.0 - self.card_selection) * 10 / fps;
			
			var index = -1;
			while ((index = index + 1) < self.card_count) {
				Console_card->var card = ?(self.cards + 8 * index);
				
				fixed var x = index * 1.0 - self.card_selection;
				fixed var importance = 1.0 - Math.min(Math.abs(x), 1.0);
				
				card_shadow.size 1.0 + importance / 3, 1.0 + importance / 3;
				card_shadow.colour shadow_r, shadow_g, shadow_b, (shadow_a * transition) * (0.5 + importance / 2);
				
				fixed var y = (-1.0 + transition) * (0.5 + importance / 2) - 0.333333;
				card_shadow.draw x, y, layer + 4;
				
				if (card.text_surface) {
					card.text_surface.size Math.pixels_x(card.text_width) * (0.333333 * 2 + importance / 3), Math.pixels_y(card.text_height) * (0.333333 * 2 + importance / 3);
					card.text_surface.colour accent_r, accent_g, accent_b, (accent_a * transition) * (0.5 + importance / 2);
					
					card.text_surface.draw x, y + 0.083333, layer + 6;
				}
			}
			
		} if (self.model_ivx) {
			self.model_camera.px = 0;
			self.model_camera.pz = 0;
			self.model_camera.py = 1.8 - transition;
			
			self.model_camera.fov = Math.TAU / 4.0 * transition;
			self.model_camera.update console_fps + 1;
			
			Gl.depth 1;
			var texture_slot = 0;
			Gl.bind_texture texture_slot, self.model_texture;
			
			model_shader.use();
			model_shader.inform model_shader_mvp_matrix, Shader.INFORM_TYPE_FIXED | Shader.INFORM_SIZE_MATRIX, self.model_camera.mvp_matrix;
			model_shader.inform model_shader_texture, Shader.INFORM_TYPE_WHOLE | Shader.INFORM_SIZE_VALUE, &texture_slot;
			
			Gl.attribute 0, ?(self.model_ivx.attribute_components + 00), ?(self.model_ivx.attribute_pointers + 00); // vertex position
			Gl.attribute 1, ?(self.model_ivx.attribute_components + 08), ?(self.model_ivx.attribute_pointers + 08); // texture coords
			Gl.attribute 2, ?(self.model_ivx.attribute_components + 16), ?(self.model_ivx.attribute_pointers + 16); // vertex normals
			
			Gl.draw self.model_ivx.index_count, self.model_ivx.index_pointer;
			Gl.depth 0;
			
		} if (self.paragraph_surface) {
			self.paragraph_surface.size Math.pixels_x(self.paragraph_width), Math.pixels_y(self.paragraph_height);
			self.paragraph_surface.colour accent_r, accent_g, accent_b, accent_a * transition;
			
			if (self.title_surface) self.paragraph_surface.draw 0, -1.0 + transition / 3 * 2, layer + 10;
			else                    self.paragraph_surface.draw 0, -1.0 + transition, layer + 10;
			
		} if (self.title_surface) {
			self.title_surface.size Math.pixels_x(self.title_width), Math.pixels_y(self.title_height);
			self.title_surface.colour accent_r, accent_g, accent_b, accent_a * transition;
			
			if (self.paragraph_surface or self.card_count or self.model_ivx) self.title_surface.draw 0, 1.0 - transition / 3, layer + 11;
			else                                                             self.title_surface.draw 0, transition - 1.0, layer + 11;
		}
	}
	
	func next(var next_page) self.next_page = next_page;
	func prev(var prev_page) self.prev_page = prev_page;
	
	func set_next(var function, var argument) {
		self.next_function = function;
		self.next_argument = argument;
	}
	
	func set_prev(var function, var argument) {
		self.prev_function = function;
		self.prev_argument = argument;
	}
	
	func set_cards(var count) {
		self.card_count = count;
		self.cards = new(8 * self.card_count);
		
		var index = -1;
		while ((index = index + 1) < self.card_count) {
			?(self.cards + 8 * index) = new(Console_card);
		}
	}
	
	func set_card_next(var index, var next_page) {
		Console_card->var card = ?(self.cards + 8 * index);
		card.next_page = next_page;
	}
	
	func set_card_text(var index, var text) {
		Console_card->var card = ?(self.cards + 8 * index);
		
		if (not card.text_surface) {
			card.text_surface = new(Surface).construct();
		}
		
		card.text_surface.texture card.text_texture = main_font.create_texture(text, 0.285714);
		
		card.text_width  = main_font.width;
		card.text_height = main_font.height;
	}
	
	func set_card_select(var index, var function, var argument) {
		Console_card->var card = ?(self.cards + 8 * index);
		
		card.select_function = function;
		card.select_argument = argument;
	}
	
	func set_card_next_platform_command(var index, var command) {
		Console_card->var card = ?(self.cards + 8 * index);
		card.next_platform_command = command;
	}
	
	func set_model(var ivx_path, var ipx_path) {
		self.model_camera = new(Camera).construct(Camera.TYPE_ORBIT, console_joystick);
		self.model_ivx = new(Ivx).construct(ivx_path);
		self.model_texture = Ipx.create_texture(ipx_path);
	}
	
	func set_title(var text) {
		if (not self.title_surface) {
			self.title_surface = new(Surface).construct();
		}
		
		self.title_surface.texture self.title_texture = main_font.create_texture(text, 0.4);
		
		self.title_width  = main_font.width;
		self.title_height = main_font.height;
	}
	
	func set_paragraph(var text) {
		if (not self.paragraph_surface) {
			self.paragraph_surface = new(Surface).construct();
		}
		
		self.paragraph_surface.texture self.paragraph_texture = main_font.create_texture(text, 0.133334);
		
		self.paragraph_width  = main_font.width;
		self.paragraph_height = main_font.height;
	}
	
	func construct {
		self.target_visibility = CONSOLE_PAGE_HIDDEN;
		self.transition = 0;
		return self;
	}
}
