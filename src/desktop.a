
print "[CONSOLE_OS] Loading desktop ...\n";

Surface->var wallpaper = new(Surface).construct();
wallpaper.texture Bmp.create_texture(CONSOLE_WALLPAPER_PATH);

Surface->var frost = new(Surface).construct();
frost.texture Bmp.create_texture(CONSOLE_FROST_PATH);
frost.layer 2;

Surface->var tint = new(Surface).construct();
tint.texture Bmp.create_texture(CONSOLE_TINT_PATH);
tint.layer 3;

Surface->var app_hint = new(Surface).construct();
var app_hint_text = "Press " ++ console_input_button_down_name() ++ " to see your apps";

main_font.size PRECISION / 15;
app_hint.texture main_font.create_texture(app_hint_text);
app_hint.layer 1;

var app_hint_text_width  = main_font.get_width (app_hint_text);
var app_hint_text_height = main_font.get_height(app_hint_text);

Surface->var time = new(Surface).construct();
var time_text = "13:37";

main_font.size PRECISION / 2;
time.texture main_font.create_texture(time_text);
time.layer 1;

var time_text_width  = main_font.get_width (time_text);
var time_text_height = main_font.get_height(time_text);

var frost_amount = PRECISION;
var frost_target = PRECISION;

func console_desktop_show(var shown) {
	frost_target = (1 - shown) * PRECISION;
}

import pages/apps.a
console_pages_apps_init();

import pages/setup.a
console_pages_setup_init();

func draw_desktop(var fps) {
	frost_amount = frost_amount + (frost_target - frost_amount) * 5 / fps;
	wallpaper.draw 0, 0;
	
	app_hint.colour accent_r, accent_g, accent_b, Math.mul_float(accent_a, (PRECISION - frost_amount) / 3 * 2);
	app_hint.size Math.pixels_x(app_hint_text_width), Math.pixels_y(app_hint_text_height);
	app_hint.draw 0, -PRECISION + (PRECISION - frost_amount) / 12 + Math.pixels_y(app_hint_text_height) / 2;
	
	time.colour accent_r, accent_g, accent_b, Math.mul_float(accent_a, (PRECISION - frost_amount));
	time.size Math.pixels_x(time_text_width), Math.pixels_y(time_text_height);
	time.draw -PRECISION + (PRECISION - frost_amount) / 12 + Math.pixels_x(time_text_width) / 2, PRECISION - PRECISION / 12 - Math.pixels_y(time_text_height) / 2;
	
	frost.colour PRECISION, PRECISION, PRECISION, frost_amount;
	frost.draw 0, 0;
	
	tint.colour window_r, window_g, window_b, Math.mul_float(window_a, frost_amount);
	tint.draw 0, 0;
	
	console_pages_setup_draw fps;
	console_pages_apps_draw fps;
	
	if (frost_amount < PRECISION / 16 and console_input_button_down()) {
		frost_target = PRECISION;
		console_pages_apps_show();
	}
}
