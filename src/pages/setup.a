
print "[CONSOLE_OS] Setting up setup page ...\n";

Console_page->var console_pages_setup_welcome;
Console_page->var console_pages_setup_navigating;
Console_page->var console_pages_setup_theme_selection;
Console_page->var console_pages_setup_wifi_selection;
Console_page->var console_pages_setup_wifi_password;
Console_page->var console_pages_setup_developer_info;
Console_page->var console_pages_setup_developer_dialog;
Console_page->var console_pages_setup_developer_confirmation;
Console_page->var console_pages_setup_done;

func console_pages_setup_disable_developer {
	print "[CONSOLE_OS] Disabling developer mode ...\n";
	
	var old_user_developer = user_developer;
	if (old_user_developer != (user_developer = 0)) save_user_data();
	
	console_pages_setup_wifi_password.next console_pages_setup_done;
	console_pages_setup_done.prev console_pages_setup_wifi_password;
}

func console_pages_setup_init {
	console_pages_setup_welcome         = new(Console_page).construct();
	console_pages_setup_navigating      = new(Console_page).construct();
	console_pages_setup_theme_selection = new(Console_page).construct();
	console_pages_setup_wifi_selection  = new(Console_page).construct();
	console_pages_setup_wifi_password   = new(Console_page).construct();
	console_pages_setup_done            = new(Console_page).construct();
	
	console_pages_setup_welcome.set_title "Welcome, " ++ user_name;
	console_pages_setup_welcome.set_paragraph "Press " ++ console_input_button_down_name() ++ " to continue";
	
	console_pages_setup_welcome.next console_pages_setup_navigating;
	console_pages_setup_navigating.set_title "Navigating menus";
	console_pages_setup_navigating.set_paragraph "Press " ++ console_input_button_left_name() ++ " to select the element on the left\nPress " ++ console_input_button_right_name() ++ " to select the element on the right\nPress " ++ console_input_button_up_name() ++ " to go back";
	
	console_pages_setup_navigating.next console_pages_setup_theme_selection;
	console_pages_setup_theme_selection.set_title "Choose a theme";
	console_pages_setup_theme_selection.set_cards console_theme_count;
	
	var index = -1;
	while ((index = index + 1) < console_theme_count) {
		console_pages_setup_theme_selection.set_card_text index, (Console_theme->?(console_theme_list + 8 * index)).name;
		console_pages_setup_theme_selection.set_card_select index, console_set_theme, ?(console_theme_list + 8 * index);
	}
	
	console_pages_setup_theme_selection.next console_pages_setup_wifi_selection;
	console_pages_setup_wifi_selection.set_title "Choose a WiFi network";
	console_pages_setup_wifi_selection.set_cards 3;
	
	console_pages_setup_wifi_selection.set_card_text 0, "WIFI_0";
	console_pages_setup_wifi_selection.set_card_text 1, "WIFI_1";
	console_pages_setup_wifi_selection.set_card_text 2, "WIFI_2";
	
	console_pages_setup_wifi_selection.next console_pages_setup_wifi_password;
	console_pages_setup_wifi_password.set_title "TODO enter wifi password";
	
	console_pages_setup_developer_info         = new(Console_page).construct();
	console_pages_setup_developer_dialog       = new(Console_page).construct();
	console_pages_setup_developer_confirmation = new(Console_page).construct();
	
	if (user_developer) {
		console_pages_setup_wifi_password.next console_pages_setup_developer_info;
		console_pages_setup_developer_info.set_title "You are a developer";
		console_pages_setup_developer_info.set_paragraph "You have chosen to become a developer.\nPlease bear in mind what this entails:\n - You accept to provide crash logs and any data concerning the console to RetroGameBox\n - You accept to receive potentially unstable beta updates\n - You accept to have full control over your system configuration files, and thus void your warranty\nIf you wish to cancel your developer membership, or have enabled it by accident, please cancel in the following page.";
		
		console_pages_setup_developer_info.next console_pages_setup_developer_dialog;
		console_pages_setup_developer_dialog.set_title "Would you like to be a developer?";
		console_pages_setup_developer_dialog.set_cards 2;
		
		console_pages_setup_developer_dialog.set_card_text 0, "No...";
		console_pages_setup_developer_dialog.set_card_text 1, "Yes!";
		
		console_pages_setup_developer_dialog.set_card_next 0, console_pages_setup_developer_confirmation;
		console_pages_setup_developer_dialog.set_card_next 1, console_pages_setup_done;
		
		console_pages_setup_developer_confirmation.set_next console_pages_setup_disable_developer;
		console_pages_setup_developer_confirmation.set_title "You will no longer be a developer";
		console_pages_setup_developer_confirmation.set_paragraph "You can always become a developer again by going into the settings application.";
		
		console_pages_setup_developer_confirmation.next console_pages_setup_done;
		
	} else {
		console_pages_setup_disable_developer();
	}
	
	console_pages_setup_done.set_title "All done!";
	console_pages_setup_done.set_paragraph "Enjoy your new RetroGameBox.";
}

func console_pages_setup_show {
	console_pages_setup_welcome.show 0;
}

func console_pages_setup_draw(var fps) {
	console_pages_setup_welcome.draw                fps;
	console_pages_setup_navigating.draw             fps;
	console_pages_setup_theme_selection.draw        fps;
	console_pages_setup_wifi_selection.draw         fps;
	console_pages_setup_wifi_password.draw          fps;
	console_pages_setup_developer_info.draw         fps;
	console_pages_setup_developer_dialog.draw       fps;
	console_pages_setup_developer_confirmation.draw fps;
	console_pages_setup_done.draw                   fps;
}
